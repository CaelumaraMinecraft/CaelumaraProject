plugins {
    id("java-library")
    alias(libs.plugins.kotlin)
    alias(libs.plugins.shadow)
}

group = "net.aurika.auspice.bukkitlegacy"

repositories {
}

subprojects {
}

dependencies {

    // 向上传递依赖项
    implementation(api(project(":Auspice:game:bukkit-legacy:services")))
    implementation(api(project(":base")))

    //Engines
    implementation(libs.xseries)
    implementation(libs.yamlengine)

    implementation(libs.caffeine)
    implementation(libs.guava)

    //Classpath libs
    compileOnly("org.checkerframework:checker-qual:3.46.0")
    compileOnly(libs.server.legacy.spigot)
    compileOnly(libs.annotations.jetbrains)
}

def targetJavaVersion = 17

java {
    JavaVersion javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks {

    build {
        dependsOn("shadowJar")
    }

    compileKotlin {

        kotlinOptions {
            jvmTarget = 17
        }

    }

    compileJava {
        options.encoding = 'UTF-8'

        if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
            options.release.set(targetJavaVersion)
        }
    }

    shadowJar {

        enabled = true

        archiveBaseName = "Auspice-CraftBukkit"
        archiveClassifier = null

        relocate("kotlin", "net.aurika.auspice.libs.kotlin")
        relocate("com.github.benmanes.caffeine", "net.aurika.auspice.libs.caffeine")
        relocate("org.snakeyaml.engine", "net.aurika.auspice.libs.snakeyaml")
        relocate("com.google.common", "net.aurika.auspice.libs.google.common")
        relocate("com.cryptomorin.xseries", "net.aurika.auspice.libs.xserise")
        relocate("org.objectweb.asm", "net.aurika.auspice.libs.asm")

//        exclude(
//                "kotlin-stdlib-2.0.20-Beta2.jar",
//                "annotations-13.0.jar",
//                "checker-qual-3.46.0.jar",
//                "snakeyaml-2.2.jar"
//        )

    }

    jar {
        enabled = false
    }

}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset "UTF-8"
    filesMatching("plugin.yml") {
        expand props
    }
}

